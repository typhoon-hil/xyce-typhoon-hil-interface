* Pulse Width Modulator Model

.SUBCKT pwm duty compout out
+PARAMS: VMAX=1 TS=0.001 DT=0 RES_OUT=1

* Duty-cycle port aux resistor
R_auxDUTY duty 0 0.001

* Triangular Voltage Source
V_Tri tri_pos 0 PWL 0S 0V {0.5*TS} 1V {TS} 0V R=0
R_auxTri tri_pos 0 1e-7

* Dead-time Auxiliary Pulse Voltage Sources
V_DTOUT n_dtout 0 PULSE V1=0 V2=DT PW={0.5*TS} PER={TS}
R_auxDTOUT n_dtout 0 1e-7
V_DTCOMPOUT n_dtcomp 0 PULSE V1=-DT V2=0 PW={0.5*TS} PER={TS}
R_auxDTCOMPOUT n_dtcomp 0 1e-7

* Non-linear Voltage Sources are used for the output generation
* smoothbsrc/rcconst help with convergence, avoiding an infinite slope

B_OUT out 0 V={IF( (V(duty) > V(tri_pos) + V(n_dtout)), VMAX, 0 )} smoothbsrc=0
+ rcconst = {TS*5e-4}
*R_PWMOUT out 0 {RES_OUT}

B_COMPOUT compout 0 V={IF( (V(duty) < V(tri_pos) + V(n_dtcomp)), VMAX, 0 )} smoothbsrc=1
+ rcconst = {TS*5e-4}
*R_PWMCOMPOUT compout 0 {RES_OUT}

.ENDS pwm
